pipeline {
    agent any

    environment {
        APP_NAME = "smartfarm-backend"
        STATE_FILE = "last_successful_prod.env"
    }

    stages {

        stage('Approval') {
            steps {
                input message: 'Approve deployment to PRODUCTION?'
            }
        }

        stage('Checkout Code') {
            steps {
                checkout scm
            }
        }

        stage('Load Image Tag from CI') {
            steps {
                copyArtifacts(
                    projectName: 'CI',
                    selector: lastSuccessful(),
                    filter: 'image.env'
                )
                script {
                    def props = readProperties file: 'image.env'
                    env.IMAGE_TAG = props.IMAGE_TAG
                }
            }
        }

        stage('Prepare Environment') {
            steps {
                withCredentials([file(credentialsId: 'smartfarm-env-prod', variable: 'ENV_FILE')]) {
                    bat '''
                    echo Preparing production .env file
                    copy %ENV_FILE% deploy\\production\\.env.production
                    '''
                }
            }
        }

        stage('Deploy to Production') {
            steps {
                dir('deploy/production') {
                    bat '''
                    echo Deploying image %APP_NAME%:%IMAGE_TAG%
                    docker compose down || exit /b 0
                    set IMAGE_TAG=%IMAGE_TAG%
                    docker compose up -d
                    '''
                }
            }
        }

        stage('Verify Health') {
            steps {
                timeout(time: 2, unit: 'MINUTES') {
                    waitUntil {
                        script {
                            def status = bat(
                                script: "docker inspect --format=\"{{.State.Health.Status}}\" smartfarm-api-prod",
                                returnStdout: true
                            ).trim()
                            return status == "healthy"
                        }
                    }
                }
            }
        }

        stage('Persist Successful Version') {
            steps {
                bat '''
                echo IMAGE_TAG=%IMAGE_TAG% > %STATE_FILE%
                '''
            }
        }
    }

    post {
        success {
            echo "Production deployment successful. Image: ${IMAGE_TAG}"
        }
        failure {
            echo "Production failed - rolling back"

            bat '''
            if exist %STATE_FILE% (
              for /f "tokens=2 delims==" %%i in (%STATE_FILE%) do set ROLLBACK_TAG=%%i
              echo Rolling back to %ROLLBACK_TAG%

              cd deploy\\production
              docker compose down || exit /b 0
              set IMAGE_TAG=%ROLLBACK_TAG%
              docker compose up -d
            ) else (
              echo No rollback version available
            )
            '''
        }
    }
}
